// Copyright (c) 2018 Recluse Project. All rights reserved.
#version 430
#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_ARB_compute_shader : enable


// Global const buffer ALWAYS bound to descriptor set 0, or the 
// first descriptor set.
layout (set = 0, binding = 0) uniform GlobalBuffer {
  mat4  view;
  mat4  proj;
  mat4  invView;
  mat4  invProj;
  mat4  viewProj;
  mat4  invViewProj;
  vec4  cameraPos;
  vec4  l_plane;
  vec4  r_plane;
  vec4  t_plane;
  vec4  b_plane;
  vec4  n_plane;
  vec4  f_plane;
  vec4  clipPlane0;
  vec2  mousePos;
  ivec2 screenSize;
  vec4  vSun; // Sundir.xyz and w is brightness.
  vec4  vAirColor;
  float fEngineTime; // total current time of the engine. 
  float fDeltaTime; // elapsed time between frames.
  float gamma;
  float exposure;
  float fRayleigh;
  float fMie;
  float fMieDist;
  float fScatterStrength;
  float fRayleighStength;
  float fMieStength;
  float fIntensity;
  float zNear;
  float zFar;
  int   bloomEnabled;
  int   enableShadows;
  int   enableAA;
} gWorldBuffer;


// The particle and it's information.
struct Particle {
  vec4    position;
  vec4    velocity;
  vec4    initVelocity;
  vec4    acceleration;
  vec4    color;
  float   size;
  float   angle;
  float   weight;
  float   life;
};

layout (set = 1, binding = 0) uniform ParticleConfig {
  float level[16];
  mat4  model;
  mat4  modelView;
  vec4  hasTexture;
  vec4  globalScale;
  vec4  lightFactor;
  float fadeAt;
  float fadeThreshold;
  float angleThreshold;
  float rate;
  float lifeTimeScale;
  float particleMaxAlive;
  float maxParticles;
  float isWorldSpace;
} particleConfig;


layout (set = 1, binding = 2) buffer ParticleBuffer {
  Particle  particles[ ];
} particleBuffer;


layout (local_size_x = 256, local_size_y = 1, local_size_z = 1) in;
void main()
{
  float bufferSz = particleConfig.maxParticles;
  for (int i = 0; (gl_GlobalInvocationID.x + i) < bufferSz; i += 256) {
    uint idx = gl_GlobalInvocationID.x + i;
    Particle p = particleBuffer.particles[idx];
    if (p.life >= 0.0) {
      p.life -= gWorldBuffer.fDeltaTime * particleConfig.rate;
      if (p.life > 0.0) {
        // Update the particle as it is still alive.
        p.velocity += gWorldBuffer.fDeltaTime * p.acceleration;
        p.position += p.velocity * gWorldBuffer.fDeltaTime;
      } else {
        // reset.
        mat4 model = particleConfig.model;
        p.life = particleConfig.particleMaxAlive * particleConfig.lifeTimeScale;
        p.velocity = p.initVelocity;
        p.position = vec4(model[3][0], model[3][1], model[3][2], model[3][3]);
      }
    }
    particleBuffer.particles[idx] = p;
  }
}



